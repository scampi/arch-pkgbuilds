pkgname=debian-byzanz
pkgver=r110.1f9e1b5
pkgrel=1
pkgdesc="Record what is happening on your desktop"
arch=('i686' 'x86_64')
url="https://salsa.debian.org/debian/byzanz"
license=('GPL')
depends=('gtk3' 'gst-plugins-base' 'gst-plugins-good')
makedepends=('git')
install=$pkgname.install
source=($pkgname::git+https://salsa.debian.org/debian/byzanz.git#tag=debian/0.3.0+git20160312-2)
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $pkgname

  patch -Np1 -i debian/patches/Fix-FTBFS-because-of-Wcast-align-error-flag.patch
  patch -Np1 -i debian/patches/audio.patch
  patch -Np1 -i debian/patches/no-Werror.patch
  patch -Np1 -i debian/patches/remove-deprecated-gnome-common-macros.patch
}

build() {
  cd $pkgname

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
               --disable-schemas-compile
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
