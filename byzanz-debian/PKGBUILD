pkgname=debian-byzanz
pkgver=r98.7c4d8b3
pkgrel=1
pkgdesc="Record what's happening on your desktop"
arch=('i686' 'x86_64')
url="http://git.gnome.org/browse/byzanz/"
license=('GPL')
depends=('gtk3' 'gst-plugins-base' 'gst-plugins-good')
makedepends=('git')
install=$pkgname.install
source=($pkgname::git://anonscm.debian.org/collab-maint/byzanz.git#commit=7c4d8b37c9b3f0c1ee351e3a5c45f78ae274fad5)
md5sums=('SKIP')

pkgver() {
  cd "$pkgname"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd $pkgname

  patch -Np1 -i debian/patches/Fix-FTBFS-because-of-Wcast-align-error-flag.patch
  patch -Np1 -i debian/patches/audio.patch
  patch -Np1 -i debian/patches/no-Werror.patch
}

build() {
  cd $pkgname

  ./autogen.sh --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
               --disable-schemas-compile
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
