pkgname=yfful-neovim
pkgver=0.3.4.r1259.gdd94165f4
pkgrel=1
pkgdesc='Fork of Vim aiming to improve user experience, plugins, and GUIs.'
arch=('i686' 'x86_64')
url='https://neovim.io'
license=('custom:neovim')
source=("${pkgname}::git+https://github.com/neovim/neovim.git#branch=master")
sha256sums=('SKIP')
provides=("neovim=${pkgver}")
conflicts=('neovim')
depends=(
  'python-pip' # for deoplete
  'python-neovim'
)
makedepends=('git' 'ninja')
options=(!strip)
install=${pkgname}.install

pkgver() {
  cd "${pkgname}"
  git describe --long | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "$pkgname"
  eval "$(luarocks path --lua-version=5.1)"
  make CMAKE_BUILD_TYPE="Release" CMAKE_EXTRA_FLAGS="-DCMAKE_INSTALL_PREFIX=/usr"
}

check() {
  cd "${pkgname}/build"
  ./bin/nvim --version
  ./bin/nvim --headless -u NONE -i NONE -c ':quit'
}

package() {
  cd "${pkgname}"
  make DESTDIR="${pkgdir}" install
  install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim:set sw=2 sts=2 et:
